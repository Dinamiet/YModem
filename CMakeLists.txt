cmake_minimum_required(VERSION 3.20.0)

project(
    YModem
)

if (NOT TARGET ${PROJECT_NAME})

if (CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)

    # Let's nicely support folders in IDEs
    set_property(GLOBAL PROPERTY USE_FOLDERS ON)

    add_compile_options(
        "-Wall" # Enable all common warnings
        "-Wextra" # Enable more warnings
        "-Wpedantic" # Warn about non-standard code
        "-Werror" # Warnings are treated as errors
        "-Wshadow" # Warn about shadow variables
        "-Wdouble-promotion" # Warn when promoting floats to double
        "-Wformat=2" # printf and scanf warnings
        "-Wformat-security" # printf and scanf warnings
        "-Wformat-truncation" # printf and scanf warnings
        "-Wundef" # Warn when evaluating not defined flags
        "-fno-common" # Warn about multiple global variables with same name
        # "-Wconversion" # Warn about implicit conversions
        "-Wpadded" # Warn about padded structures
        "-flto" # Link time optimization
        "-fno-fat-lto-objects" # Link time optimization
        "-ffunction-sections" # Link time optimization help
        "-fdata-sections" # Link time optimization help
    )

    add_link_options(
        "-Wl,--gc-sections" # Remove unwanted sections
        "-flto" # Link time optimization
        "-fno-fat-lto-objects" # Link time optimization
    )

    include(CTest)

    add_subdirectory(app)

endif()

add_library(
    ${PROJECT_NAME}
    STATIC
)

target_include_directories(
    ${PROJECT_NAME}
    PUBLIC
        include
)

target_sources(
    ${PROJECT_NAME}
    PUBLIC  # All public source and header files listed here
        include/ymodem.h
    PRIVATE # All private source and header files listed here
        src/ymodem_common.h
        src/ymodem_receive.c
        src/ymodem_transmit.c
)

add_subdirectory(lib/ChecksumHash)
target_link_libraries(${PROJECT_NAME} PRIVATE ChecksumHash)

endif()
