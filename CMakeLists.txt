cmake_minimum_required(VERSION 3.20.0)

project(
    YModem
)

if (NOT TARGET ${PROJECT_NAME})

if (CMAKE_PROJECT_NAME STREQUAL PROJECT_NAME)

    # Let's nicely support folders in IDEs
    set_property(GLOBAL PROPERTY USE_FOLDERS ON)

    # Enable link time optimization if supported by the compiler
    include(CheckIPOSupported)
    check_ipo_supported(RESULT IPO_SUPPORTED)
    if(IPO_SUPPORTED)
        set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
    endif()

    include(CTest)

    add_subdirectory(app)

endif()

add_library(
    ${PROJECT_NAME}
    STATIC
)

target_compile_options(${PROJECT_NAME} PRIVATE -Wall -Wextra -Wpedantic -Werror)

target_include_directories(
    ${PROJECT_NAME}
    PUBLIC
        include
)

target_sources(
    ${PROJECT_NAME}
    PUBLIC  # All public source and header files listed here
        include/ymodem.h
    PRIVATE # All private source and header files listed here
        src/ymodem_common.h
        src/ymodem_receive.c
        src/ymodem_transmit.c
)

add_subdirectory(extern/ChecksumHash)
target_link_libraries(${PROJECT_NAME} PRIVATE ChecksumHash)

endif()
